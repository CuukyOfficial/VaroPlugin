import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java-library'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

sourceSets {
	main {
		java {
			srcDirs 'src'
		}

		resources {
			srcDirs 'resources'
		}
	}
}

repositories {
	jcenter()
	flatDir {
	   	dirs 'libs'
	}
	maven {
		url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	}

	maven {
		url 'https://maven.sk89q.com/repo/'
	}

	maven {
		url 'https://jitpack.io'
	}
}

configurations {
	internalLibs
}

dependencies {
	compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
	compileOnly 'com.googlecode.json-simple:json-simple:1.1.1'
	compileOnly ('com.sk89q.worldedit:worldedit-bukkit:7.3.0-SNAPSHOT') {changing = true}
	compileOnly ('com.github.LabyMod:labymod-server-api:master-SNAPSHOT') {changing = true}

	compileOnly 'net.dv8tion:JDA:4.2.0_229'
	compileOnly 'com.github.pengrad:java-telegram-bot-api:5.0.1'

	internalLibs ('com.github.CuukyOfficial:VaroBanAPI-Client:master-SNAPSHOT') {changing = true}
	if(file("libs/CFW - Cuuky FrameWork.jar").exists())
		internalLibs name: 'CFW - Cuuky FrameWork'
	else
		internalLibs ('com.github.CuukyOfficial:CFW:master-SNAPSHOT') {changing = true}

	compileOnly configurations.internalLibs
}

jar {
	manifest {
		attributes(
			'Manifest-Version': '1.0',
			'Class-Path': '.',
			'Main-Class': 'de.cuuky.varo.MainLauncher',
		)
	}

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	from {
		configurations.internalLibs.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
}

processResources {
	from(sourceSets.main.resources.srcDirs) {
		filter ReplaceTokens, tokens: [name: "Varo"]
		filter ReplaceTokens, tokens: [version: "4.5.5"]
		filter ReplaceTokens, tokens: [author: "Cuuky"]
	}
}