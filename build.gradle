import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java-library'
apply plugin: 'maven-publish'

group 'de.varoplugin'
version '4.10.0'

def tmpSrcFolder = 'tmpSrc'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

sourceSets {
    main {
        java {
            srcDirs 'src'
        }

        resources {
            srcDirs 'resources'
        }
    }
    
    test {
    	java {
            srcDirs 'test'
        }
    }
}

repositories {
	mavenCentral()
	
    flatDir {
        dirs 'libs'
    }
    
    maven {
    	url 'https://repo.varoplugin.de/repository/maven-public/'
    }
    
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
	
	maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        url 'https://maven.enginehub.org/repo/'
    }
    
    maven {
    	url 'https://m2.dv8tion.net/releases'
    }
    
    maven {
        url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

configurations {
    internalLibs
    implementation.extendsFrom(internalLibs)
}

def JDA_version = '5.0.0-alpha.12'
def JDA_sha512 = 'daef57d9536338685143d0c058dff451eb7ca7d807a04f1cc320c762f5eb65fd120677650de6ac4579893fcec58d21dc68559a1dbf9239b2a2d7333ed85e3a14'
def SimpleYaml_version = '1.8'
def SimpleYaml_sha512 = '041d3b34ca5a588afcd9381fd7eb6092acdad188f83013879640cb221092b34d56fbb9f7956c7919888efc88faf14489b88661701ffd33d3abc637f610042c4e'
def SnakeYaml_version = '1.30'
def SnakeYaml_sha512 = 'b00f52326cae804d0dbb48c0ed7f3a98cdebbce9b145f685c616e4049b65183a18e98ca29b7b0275971f9ece52138d0015bb9771902532084cb2cc07a264cfc6'

dependencies {
	testImplementation('org.junit.jupiter:junit-jupiter-api:5.8.2')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.8.2')

    this.addModularImplementation('org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT', 'spigot', false)
    this.addModularCompile('com.googlecode.json-simple:json-simple:1.1.1', 'gson', false)
    this.addModularCompile('com.sk89q.worldedit:worldedit-bukkit:6.1.3-SNAPSHOT', 'worldedit', true)
    this.addModularCompile('com.github.labymod:legacy-labymod-server-api:1.0', 'labymodapi', true)
    this.addModularCompile("net.dv8tion:JDA:${JDA_version}", 'jda', false)
    this.addModularCompile('com.github.pengrad:java-telegram-bot-api:5.4.0', 'telegramapi', false)
    this.addModularCompile('me.clip:placeholderapi:2.10.10', 'placeholderapi', false)
    this.addModularImplementation("me.carleslc.Simple-YAML:Simple-Yaml:${SimpleYaml_version}", 'simple-yaml', false)

    this.addModularInternal('de.varoplugin:CFW:1.0.0', 'CFW', true)
    this.addModularInternal('de.varoplugin:CFW:0.6.14', 'CFW-legacy', true)
}

boolean checkLib(String filePath) {
    return file('libs/' + filePath + '.jar').exists()
}

void addModularCompile(String repoName, String fileName, boolean changingB) {
    if (this.checkLib(fileName))
        this.dependencies.compileOnly name: fileName
    else
        this.dependencies.compileOnly (repoName) { changing = changingB }
}

void addModularImplementation(String repoName, String fileName, boolean changingB) {
    if (this.checkLib(fileName))
        this.dependencies.implementation name: fileName
    else
        this.dependencies.implementation (repoName) { changing = changingB }
}

void addModularInternal(String repoName, String fileName, boolean changingB) {
    if (this.checkLib(fileName))
        this.dependencies.internalLibs name: fileName
    else
        this.dependencies.internalLibs (repoName) { changing = changingB }
}

jar {
	if (project.hasProperty('destinationDir'))
		destinationDir = file(project.property('destinationDir').toString())

	if (project.hasProperty('fileName'))
		archiveFileName = project.property('fileName').toString()

    manifest {
        attributes(
                'Manifest-Version': '1.0',
                'Class-Path': '.',
                'Main-Class': 'de.varoplugin.varo.RunnableLauncher',
        )
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.internalLibs.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from (sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [name: 'Varo']
        filter ReplaceTokens, tokens: [version: project.version]
        filter ReplaceTokens, tokens: [author: 'Cuuky']
    }
}

task copySource(type: Copy) {
	from (sourceSets.main.java.srcDirs) {
		filter ReplaceTokens, tokens: [JDA_version: JDA_version + '-withDependencies-no-opus']
        filter ReplaceTokens, tokens: [JDA_hash: JDA_sha512.decodeHex().encodeBase64().toString()]
        filter ReplaceTokens, tokens: [SimpleYaml_version: SimpleYaml_version]
        filter ReplaceTokens, tokens: [SimpleYaml_hash: SimpleYaml_sha512.decodeHex().encodeBase64().toString()]
        filter ReplaceTokens, tokens: [SnakeYaml_version: SnakeYaml_version]
        filter ReplaceTokens, tokens: [SnakeYaml_hash: SnakeYaml_sha512.decodeHex().encodeBase64().toString()]
	}
	into "${buildDir}/${tmpSrcFolder}"
}

copySource.mustRunAfter(clean)

compileJava {
	source = "${buildDir}/${tmpSrcFolder}"
	
	dependsOn(clean)
	dependsOn(copySource)
}

java {
	withJavadocJar()
	withSourcesJar()
}

javadoc {
    options.encoding = 'UTF-8'
}

test {
	useJUnitPlatform()

	testLogging {
        exceptionFormat = 'full'
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			
			pom {
                name = 'VaroPlugin'
                url = 'https://varoplugin.de'
			}
		}
	}

	repositories {
		maven {
			url 'https://repo.varoplugin.de/repository/maven-releases/'
			credentials {
                username project.hasProperty('repouser') ? project.property('repouser').toString() : null
                password project.hasProperty('repopassword') ? project.property('repopassword').toString() : null
            }
		}
	}
}
